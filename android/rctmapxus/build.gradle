apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet("compileSdkVersion", 30)
    buildToolsVersion safeExtGet("buildToolsVersion", '30.0.2')

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 23)
        targetSdkVersion safeExtGet('targetSdkVersion', 30)
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // React Native
    implementation "com.facebook.react:react-native:+"
    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //mapxus
    implementation("com.mapxus.map:mapxusmap:4.2.4")
    implementation ("com.mapxus.positioning:positioning:2.0.9")
    implementation ("com.mapxus.visual:mapxusvisual:0.2.5")

    // Mapbox SDK
    if (rootProject.ext.has('rnmbglMapboxLibs')) {
        rootProject.ext.get('rnmbglMapboxLibs').split(';').each {
            implementation it
        }
    } else {
        implementation 'com.mapbox.mapboxsdk:mapbox-sdk-services:5.1.0'
    }

    // Dependencies
    implementation "com.android.support:support-vector-drawable:${safeExtGet('supportLibVersion', '28.0.0')}"
    implementation "com.android.support:support-annotations:${safeExtGet('supportLibVersion', '28.0.0')}"
    implementation "com.android.support:appcompat-v7:${safeExtGet('supportLibVersion', '28.0.0')}"
    implementation "com.squareup.okhttp3:okhttp:${safeExtGet('okhttpVersion', '4.9.1')}"

    // Mapbox plugins
    if (rootProject.ext.has('rnmbglMapboxPlugins')) {
        rootProject.ext.get('rnmbglMapboxPlugins').split(';').each {
            implementation it
        }
    } else {
        implementation('com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v9:0.12.0') {
            exclude group: 'com.mapbox.mapboxsdk', module: 'mapbox-android-sdk'
        }
        implementation ('com.mapbox.mapboxsdk:mapbox-android-plugin-markerview-v9:0.4.0'){
            exclude group: 'com.mapbox.mapboxsdk', module: 'mapbox-android-sdk'
        }
        implementation 'com.mapbox.mapboxsdk:mapbox-sdk-turf:5.1.0'
    }
}
